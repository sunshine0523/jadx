plugins {
	id 'application'

	// use shadow only for application scripts, jar will be copied from jadx-gui
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
	implementation(project(':jadx-core'))
	implementation(project(':jadx-plugins-tools'))

	runtimeOnly(project(':jadx-plugins:jadx-dex-input'))
	runtimeOnly(project(':jadx-plugins:jadx-java-input'))
	runtimeOnly(project(':jadx-plugins:jadx-java-convert'))
	runtimeOnly(project(':jadx-plugins:jadx-smali-input'))
	runtimeOnly(project(':jadx-plugins:jadx-rename-mappings'))
	runtimeOnly(project(':jadx-plugins:jadx-kotlin-metadata'))
	runtimeOnly(project(':jadx-plugins:jadx-script:jadx-script-plugin'))

	implementation 'com.beust:jcommander:1.82'
	implementation 'ch.qos.logback:logback-classic:1.4.7'
}

application {
	applicationName = 'jadx'
	mainClass.set('jadx.cli.JadxCLI')
	applicationDefaultJvmArgs = ['-Xms128M', '-XX:MaxRAMPercentage=70.0', '-XX:+UseG1GC']
}
shadow {
	mainClassName = application.mainClass.get()
}
shadowJar {
	// shadow jar not needed
	configurations = []
}

applicationDistribution.with {
	into('') {
		from '../.'
		include 'README.md'
		include 'NOTICE'
		include 'LICENSE'
	}
}
